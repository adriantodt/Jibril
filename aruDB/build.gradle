// Aru!DB
plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '0.9.17'
}

group = 'pw.aru'
version = '2.0.1'
//Repositories and Dependencies
repositories {
    jcenter()
    maven { url = 'https://dl.bintray.com/adriantodt/maven' }
    mavenLocal()
}

dependencies {
    //Source of memes
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.60"
    compile "org.jetbrains.kotlin:kotlin-reflect:1.2.60"

    //Client and ID generator
    compile 'redis.clients:jedis:2.9.0'
    compile 'pw.aru.snowflake:snowflake-local:1.0'

    //Data Conversion Stack
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.6'
    compile 'de.undercouch:bson4jackson:2.9.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        LocalRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version project.version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}