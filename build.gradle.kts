import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    kotlin("jvm") version "1.3.21"
    application
    id("com.github.ben-manes.versions") version "0.20.0"
}

group = "pw.aru"
version = "3.0"

repositories {
    jcenter()
    maven { setUrl("https://jitpack.io") }
    maven { setUrl("https://dl.bintray.com/adriantodt/maven") }
    maven { setUrl("https://dl.bintray.com/kodehawa/maven") }
    mavenLocal()
}

dependencies {
    compile(kotlin("stdlib-jdk8"))
    compile(kotlin("reflect"))

    compile(project("aruCore"))

    compile("com.github.mewna:catnip:1.1.0")
    compile("com.github.queer:catnip-utilities:360b876")

    // Main APIs
    compile("com.sedmelluq:lavaplayer:1.3.10")
    compile("com.github.samophis:LavaClient:4.0.0-beta1") //3.0.3
    compile("com.github.natanbc:weeb4j:3.5")

    // Useful
    //compile("com.sedmelluq:jda-nas:1.0.6")
    compile("net.kodehawa:imageboard-api:2.0.7")
    //compile("com.ztomic:wkhtmltopdf-java:0.0.1") //Maybe later

    // Open-Source Libraries
    compile("pw.aru.discord:dice-notation:1.0")
    compile("pw.aru.utils:properties:1.2")
    compile("pw.aru.snowflake:snowflake-local:1.0")
    compile("pw.aru.hungergames:hg-engine:1.0")
    compile("pw.aru.hungergames:hg-loader:1.0")
    compile("pw.aru.api:DD4J:1.0.1")

    //Helping stuff
    compile("org.apache.commons:commons-lang3:3.8.1")
    compile("org.apache.commons:commons-collections4:4.1")
    compile("org.apache-extras.beanshell:bsh:2.0b6")

    //Scanning and Injections
    compile("io.github.classgraph:classgraph:4.6.13")
    compile("org.kodein.di:kodein-di-generic-jvm:6.1.0")
    compile("pw.aru.kt:kodein-jit-bindings:2.1")

    //Logging
}

tasks.withType<KotlinCompile> {
    kotlinOptions.jvmTarget = "1.8"
}

configure<ApplicationPluginConvention> {
    mainClassName = "pw.aru.Init"
}

with(rootProject.file("src/main/kotlin/pw/aru/exported/exported.kt")) {
    parentFile.mkdirs()
    createNewFile()
    writeText(
        """
@file:JvmName("AruExported")
@file:Suppress("unused")

/*
 * file "exported.kt". DO NOT EDIT MANUALLY. THIS FILE IS GENERATED BY GRADLE.
 */

package pw.aru.exported

/**
 * Aru! Version
 */
const val aru_version = "$version"

/**
 * User Agent
 */
const val user_agent = "catnip/DiscordBot (Aru! $version)"
""".trim()
    )
}

tasks {
    register("runLocalRedis", Exec::class) {
        workingDir("db")
        commandLine("db/redis-server.exe", "./redis.conf")
    }

    register("runAndesite", Exec::class) {
        workingDir("andesite")
        commandLine("java", "-jar", "andesite-node-0.13.0-win-x86-64.jar")
    }
}

